buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

ext {
    springBootVersion = '1.2.3.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

version = '0.2.4-SNAPSHOT'

jar {
    baseName = 'spring-angular-cache-preloader'
    version = project.version
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-freemarker:${springBootVersion}")
    compile("commons-io:commons-io:1.3.2")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("commons-httpclient:commons-httpclient:3.1")
    optional("org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}")
}

compileJava.dependsOn(processResources)

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full" // show assertion errors in build
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

if (System.getenv('CI') != 'true') {

    apply plugin: 'signing'
    apply plugin: 'maven'

    group = 'eu.hinsch'

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from tasks.javadoc.destinationDir
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'spring-angular-cache-preloader'
                    packaging 'jar'
                    description 'Component to pre-fill caches for angular js rest request though index page'
                    url 'https://github.com/lukashinsch/spring-angular-cache-preloader/'

                    scm {
                        url 'scm:git@github.com:lukashinsch/spring-angular-cache-preloader.git'
                        connection 'scm:git@github.com:lukashinsch/spring-angular-cache-preloader.git'
                        developerConnection 'scm:git@github.com:lukashinsch/spring-angular-cache-preloader.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'lukashinsch'
                            name 'Lukas Hinsch'
                        }
                    }
                }
            }
        }
    }
}